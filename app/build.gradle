apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.android.extensions'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId 'net.rafaeltoledo.stak'
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 5
        versionName '0.0.5'

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        /*debug {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }*/
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    ktlint
}

final supportLibVersion = '27.0.2'
final retrofitVersion = '2.3.0'
final okHttpVersion = '3.9.1'
final espressoVersion = '3.0.1'
final testRunnerRulesVersion = '1.0.1'
final ankoVersion = '0.10.3'

dependencies {
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation 'com.google.code.gson:gson:2.8.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "org.jetbrains.anko:anko-common:$ankoVersion"
    implementation 'com.github.bumptech.glide:glide:4.4.0'
    implementation 'com.makeramen:roundedimageview:2.3.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.5.1'
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"

    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$espressoVersion") {
        exclude module: 'appcompat-v7'
        exclude module: 'support-v4'
        exclude module: 'design'
        exclude module: 'recyclerview-v7'
    }
    androidTestImplementation "com.android.support:support-annotations:$supportLibVersion"
    androidTestImplementation "com.android.support.test:runner:$testRunnerRulesVersion"
    androidTestImplementation "com.android.support.test:rules:$testRunnerRulesVersion"

    androidTestImplementation 'br.com.concretesolutions:requestmatcher:2.2.0'
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okHttpVersion"

    ktlint 'com.github.shyiko:ktlint:0.14.0'
}

task ktlint(type: JavaExec) {
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
    args 'src/**/*.kt'
}

task ktlintFormat(type: JavaExec) {
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
    args '-F', 'src/**/*.kt'
}

check.dependsOn(['ktlint', 'ktlintFormat'])

