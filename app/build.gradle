apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId 'net.rafaeltoledo.stak'
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName '0.0.2'

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        /*debug {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }*/
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    ktlint
}

final supportLibVersion = '25.3.0'
final retrofitVersion = '2.2.0'
final okHttpVersion = '3.6.0'
final espressoVersion = '2.2.2'
final testRunnerRulesVersion = '0.5'
final ankoVersion = '0.9.1a'

dependencies {
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"

    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    compile 'com.google.code.gson:gson:2.8.0'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.anko:anko-common:$ankoVersion"
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.makeramen:roundedimageview:2.3.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.3.1'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"

    androidTestCompile "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$espressoVersion") {
        exclude module: 'appcompat-v7'
        exclude module: 'support-v4'
        exclude module: 'design'
        exclude module: 'recyclerview-v7'
    }
    androidTestCompile "com.android.support:support-annotations:$supportLibVersion"
    androidTestCompile "com.android.support.test:runner:$testRunnerRulesVersion"
    androidTestCompile "com.android.support.test:rules:$testRunnerRulesVersion"

    androidTestCompile 'br.com.concretesolutions:requestmatcher:2.1.0'
    androidTestCompile "com.squareup.okhttp3:mockwebserver:$okHttpVersion"

    ktlint 'com.github.shyiko:ktlint:0.6.1'
}

task ktlint(type: JavaExec) {
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
    args 'src/**/*.kt'
}

task ktlintFormat(type: JavaExec) {
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
    args '-F', 'src/**/*.kt'
}

check.dependsOn(['ktlint', 'ktlintFormat'])

